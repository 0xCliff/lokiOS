ORG 0

BITS 16

_START:
	JMP SHORT START
	NOP

TIMES 33 DB 0	; BIOS PARAMETER BLOCK

START:
	JMP 0X7C0:STEP2
	
STEP2:	
	CLI		; CLEAR INTERUPTS
	MOV AX, 0X7C0
	MOV DS, AX
	MOV ES, AX
	MOV AX, 0X00
	MOV SS, AX
	MOV SP, 0X7C00
	STI		; ENABLES INTERUPTS
	
	MOV AH, 2	; READ SECTOR COMMAND
	MOV AL, 1	; ONE SECTOR TO READ
	MOV CH, 0	; CYLINDER LOW EIGHT BITS
	MOV CL, 2	; READ SECTOR TWO
	MOV DH, 0	; HEAD NUMBER
	MOV BX, BUFFER
	INT 0X13
	JC ERROR
	
	MOV SI, BUFFER
	CALL PRINT

	JMP $

ERROR:
	MOV SI, ERROR_MESSAGE
	CALL PRINT
	JMP $
	
PRINT:	
	MOV BX, 0

.LOOP:	
	LODSB
	CMP AL, 0
	JE .DONE

	CALL PRINT_CHAR
	JMP .LOOP

.DONE:	
	RET

PRINT_CHAR:
	MOV AH, 0EH
	INT 0X10
	RET

ERROR_MESSAGE: DB "Failed to load sector!", 0

TIMES 510-($ - $$) DB 0
DW 0XAA55

BUFFER:
	
