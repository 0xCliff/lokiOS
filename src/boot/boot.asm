ORG 0X7C00
BITS 16

CODE_SEG EQU GDT_CODE - GDT_START
DATA_SEG EQU GDT_DATA - GDT_START

_START:
	JMP SHORT START
	NOP

TIMES 33 DB 0	; BIOS PARAMETER BLOCK

START:
	JMP 0:STEP2
	
STEP2:	
	CLI		; CLEAR INTERUPTS
	MOV AX, 0X000
	MOV DS, AX
	MOV ES, AX
	MOV SS, AX
	MOV SP, 0X7C00
	STI		; ENABLES INTERUPTS

.LOAD_PROTECTED:
	CLI
	LGDT[GDT_DESCRIPTOR]
	MOV EAX, CR0
	OR EAX, 0X1
	MOV CR0, EAX
	JMP CODE_SEG:LOAD32

; GDT
GDT_START:

GDT_NULL:
	DD 0X0
	DD 0X0

; OFFSET 0X8
GDT_CODE:		; CS SHOULD POINT TO THIS
	DW 0XFFFF 	; SEGMENT LIMIT FIRST 0-15 BITS
	DW 0		; BASE FIRST 0-15 BITS
	DB 0		; BASE 16-23 BITS
	DB 0X09A	;
	DB 11001111B ; HIGH 4 BIT FLAGS AND THE LOW 4 BIT FLAGS
	DB 0		; BASE 24-31 BITS

; OFFSET 0X10
GDT_DATA:		; DS, SS, ES, FS, GS
	DW 0XFFFF	; SEGMENT LIMIT FIRST 0-15 BITS
	DW 0		; BASE FIRST 0-15 BITS
	DB 0		; BASE 16-23 BITS
	DB 0X092	;
	DB 11001111B ; HIGH 4 BIT FLAGS AND THE LOW 4 BIT FLAGS
	DB 0		; BASE 24-31 BITS

GDT_END:

GDT_DESCRIPTOR:
	DW GDT_END - GDT_START - 1
	DD GDT_START

[BITS 32]
LOAD32:
	MOV AX, DATA_SEG
	MOV DS, AX
	MOV ES, AX
	MOV FS, AX
	MOV GS, AX
	MOV SS, AX
	MOV EBP, 0X00200000
	MOV ESP, EBP
	JMP $

TIMES 510-($ - $$) DB 0
DW 0XAA55


	
